plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
}

version = "${mod_stage}" != "" ? "${mc_version}-${mod_version}-${mod_stage}" : "${mc_version}-${mod_version}"
group = "meldexun.${mod_id}"
archivesBaseName = "${mod_name}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    atDependencies
    implementation.extendsFrom(atDependencies)

    includedDependencies
    implementation.extendsFrom(includedDependencies)

    buildDependencies
}

repositories {
    flatDir { dirs 'libs' }

    ivy {
        url 'https://github.com/'

        patternLayout {
            artifact '[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]'
        }

        metadataSources { artifact() } 
    }

    maven { url = 'https://www.cursemaven.com' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    includedDependencies 'meldexun:ASMUtil:2.6.1@jar'
    includedDependencies 'meldexun:ReflectionUtil:1.0.0@jar'
    includedDependencies 'meldexun:MemoryUtil:1.1.0@jar'

    buildDependencies 'curse.maven:serene-seasons-291874:2799213'
    buildDependencies 'curse.maven:fancy-menu-367706:4655936'
    buildDependencies 'curse.maven:konkrete-410295:4514895'
    buildDependencies 'curse.maven:open-terrain-generator-265894:5286210'
    buildDependencies 'libs:OptifineDeobf:1.12.2-G5'
}

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"

    accessTransformer = files(
        'src/main/resources/META-INF/accesstransformer.cfg',
        files(configurations.atDependencies.collect { zipTree(it) }).getAsFileTree().matching {
            include 'META-INF/accesstransformer.cfg'
        }
    )

    runs {
        client {
            taskName "runClient ${mod_name} ${mc_version}"
            environment 'MC_VERSION', "${mc_version}"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'fml.coreMods.load', "${mod_plugin_class}"
        }

        server {
            taskName "runServer ${mod_name} ${mc_version}"
            environment 'MC_VERSION', "${mc_version}"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'fml.coreMods.load', "${mod_plugin_class}"
        }
    }
}

processResources {
    inputs.property('version', "${mod_stage}" != "" ? "${mod_version}-${mod_stage}" : "${mod_version}")

    filesMatching('mcmod.info') {
        expand version: inputs.properties.get('version')
    }
}

jar {
    archiveBaseName = mod_name
    archiveAppendix = mc_version
    archiveVersion = mod_version
    archiveClassifier = mod_stage

    from {
        configurations.includedDependencies.collect { it.isDirectory() ? it : zipTree(it).matching { exclude 'LICENSE' } }
    }

    manifest {
        attributes 'FMLAT': 'accesstransformer.cfg'
        attributes 'FMLCorePlugin': "${mod_plugin_class}"
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

jar.finalizedBy('reobfJar')

compileJava {
    classpath = sourceSets.main.compileClasspath + configurations.buildDependencies
}
